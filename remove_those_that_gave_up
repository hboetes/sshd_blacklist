#!/bin/zsh

# All this ugly code just to get a list of IP-adresses
# if only nft could have a simple command to list all ip adresses in a set...
# https://lore.kernel.org/netfilter-devel/CAOzo9e7yoiiTLvMj0_wFaSvdf0XpsymqUVb8nUMAuj96nPM5ww@mail.gmail.com/T/#u
source /etc/sshd_blacklist/settings

# convert 192.168.1.2-192.168.1.4 to 192.168.1.2/31 192.168.1.4/32
range2cidr() {
    ipcalc --no-decorate -d $1
}

# convert 192.168.1.2/31 to 192.168.1.2 192.168.1.3
cidr2ips() {
    ipcalc --no-decorate -S 32 $1 | sed -e 's|/32||'
}

# Get all ip addresses and ranges from the sshd_blacklist table.
ranges=$(
    for word in $(nft list set ip $name $name); do
        # This regex is not foolproof, but for our purposes it will do.
        if [[ $word =~ ^[0-9]+\.[0-9]+\.[0-9]+\.[0-9]+ ]]; then
            # Get rid of the trailing comma.
            word=${word%,}
            echo $word
        fi
    done
      )

# Wonder what an "eikel" is? It's dutch for acorn.
eikels=$(
    for element in ${=ranges}; do
        case $element in
            *-*)
                for range in $(range2cidr $element); do
                    cidr2ips $range
                done
                ;;
            */*)
                cidr2ips $element
                ;;
            *)
                echo $element
                ;;
        esac
    done
      )

# empty our log file
: > $log

# Now we have found our eikels, let's remove them if they gave up.
for eikel in ${=eikels}; do
    if ! grep -q "$name.*SRC=$eikel " $ulog; then
        echo "$eikel gave up" >> $log
        nft delete element ip $name $name "{ $eikel }"
    fi
done
